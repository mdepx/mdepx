OUTPUT_ARCH("riscv")
ENTRY(_start)

MEMORY
{
	sram0 (rwx)  : ORIGIN = 0x80000000, LENGTH = 8M
	sram1 (rwx)  : ORIGIN = 0x80800000, LENGTH = 120M /* malloc */
}

SECTIONS
{
	. = 0x80000000;

	.start . : {
		*start.o(.text)
	} > sram0

	.text : {
		*(.text)
	} > sram0

	.sysinit : {
		__sysinit_start = ABSOLUTE(.);
		*(.sysinit)
		__sysinit_end = ABSOLUTE(.);
	} > sram0

	.rodata : {
		*(.rodata)
	} > sram0

	.rodata.str1.1 : {
		*(.rodata.str1.1)
	} > sram0

	__cap_relocs :  ALIGN(16) {
		*(__cap_relocs)
	} > sram0

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.sdata : {
		_sdata = ABSOLUTE(.);
		*(.sdata)
		_edata = ABSOLUTE(.);
	} > sram0

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss COMMON)
		*(.sbss)
		_ebss = ABSOLUTE(.);
	} > sram0
}
